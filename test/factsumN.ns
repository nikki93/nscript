#A demonstration factorial function. It works by putting the numbers 1..n on the
#stack first, then multiplying them together.

{
    #If it's 0 we give 1, otherwise calculate the factorial.
    dup 0 ==
    {
        1 +
    }
    {
        #We put 1 and a block which copies and increments numbers before the count.
        1 rot { dup 1 + } rot

        #We put count - 1 on stack, and in the 'oneLess' variable.
        1 - dup &oneLess =

        #This will cause { dup 1 + } to run starting with 1, count - 1 times, thus
        #making  a list of numbers from 1 to count on the stack.
        repeat

        #Now we multiply 'oneLess' times, which multiplies the list of numbers
        #together.
        &* oneLess repeat

    } ifelse
} &fact =

"10 factorial: " print 10 fact print "\n" print

#Now we create a sumN function to sum first n numbers using this fact function.
{ &+ &* = fact } &sumN =

"Sum of integers 1 to 100: " print 100 sumN print "\n" print
